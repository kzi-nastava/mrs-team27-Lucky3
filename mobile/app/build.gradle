plugins {
    alias(libs.plugins.android.application)
    id 'com.google.gms.google-services'
}

// Read IP_ADDR from local.properties (falls back to emulator default 10.0.2.2)
// To test on a physical device, add this line to mobile/local.properties:
//   IP_ADDR=192.168.x.x    (your computer's WiFi IP)
def localProps = new Properties()
def localPropsFile = rootProject.file('local.properties')
if (localPropsFile.exists()) {
    localProps.load(new FileInputStream(localPropsFile))
}
def ipAddr = localProps.getProperty('IP_ADDR', '10.0.2.2')

android {
    namespace 'com.example.mobile'
    compileSdk {
        version = release(36)
    }

    defaultConfig {
        applicationId "com.example.mobile"
        minSdk 35
        targetSdk 36
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // IP address from local.properties (default: 10.0.2.2 for emulator)
        buildConfigField "String", "IP_ADDR", "\"${ipAddr}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "IP_ADDR", "\"${ipAddr}\""
        }
        debug {
            buildConfigField "String", "IP_ADDR", "\"${ipAddr}\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
}

dependencies {
    implementation libs.appcompat
    implementation libs.material
    implementation libs.constraintlayout
    implementation libs.lifecycle.livedata.ktx
    implementation libs.lifecycle.viewmodel.ktx
    implementation libs.navigation.fragment
    implementation libs.navigation.ui

    //for image loading
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.16.0'

    // OSM Maps
    implementation 'org.osmdroid:osmdroid-android:6.1.20'
    implementation 'com.github.MKergall:osmbonuspack:6.9.0'

    // Retrofit2 + Gson (Lecture 07 - Networking)
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'

    // Material Design
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core

    implementation platform('com.google.firebase:firebase-bom:34.9.0')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-messaging'
}